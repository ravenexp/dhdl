use ast::*;

#[LALR]
#[recursive_ascent]
grammar;

pub ModuleDecl: ModuleDecl = {
    "module" <name: ident> ";"
    <entities: EntityDecls>
    "end" "module" <endname: ident> ";"
        => ModuleDecl { name: name, entities: entities, endname: endname }
};

EntityDecls: Vec<EntityDecl> = { EntityDecl* };

EntityDecl: EntityDecl = {
    "entity" <name: ident> <ports: (PortDecls)?> ";"
    <wires: WireDecls>
    <insts: EntityInsts>
    "end" "entity" <endname: ident> ";"
        => EntityDecl {
            name: name,
            ports: ports,
            wires: wires,
            insts: insts,
            endname: endname
        }
};

PortDecls: Vec<PortDecl> = { "(" <PortDecl+> ")" };

PortDecl: PortDecl = {
    <name: ident> ":" <dir: portdir> <class: ident> ";"
        => PortDecl { name: name, dir: dir, class: class }
};

WireDecls: Vec<WireDecl> = { WireDecl* };

WireDecl: WireDecl = {
    "wire" <name: ident> ":" <class: ident> ";"
        => WireDecl { name: name, class: class }
};

EntityInsts: Vec<EntityInst> = { EntityInst* };

EntityInst: EntityInst = {
    "put" <name: ident> ":" <entity: ident>
        "(" <ports: PortAssigns> ")" ";"
        => EntityInst {
            name: name,
            entity: entity,
            ports: ports
        }
};

PortAssigns: Vec<PortAssign> = { PortAssign+ };

PortAssign: PortAssign = {
    <port: ident> <dir: portassign> <wire: ident> ";"
        => PortAssign { port: port, dir: dir, wire: wire }
};

// Terminals

portdir: Direction = {
    ()    => Direction::None,
    "in"  => Direction::In,
    "out" => Direction::Out
};

portassign: Direction = {
    "==" => Direction::None,
    "<=" => Direction::In,
    "=>" => Direction::Out
};

ident: Ident = {
    r"[a-zA-Z][a-zA-Z0-9]*" => Ident(String::from(<>))
};

number: Number = {
    r"[0-9]+" => Number(String::from(<>))
};
